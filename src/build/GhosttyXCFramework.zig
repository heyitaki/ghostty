const GhosttyXCFramework = @This();

const std = @import("std");
const Config = @import("Config.zig");
const SharedDeps = @import("SharedDeps.zig");
const GhosttyLib = @import("GhosttyLib.zig");
const XCFrameworkStep = @import("XCFrameworkStep.zig");
const Target = @import("xcframework.zig").Target;

xcframework: *XCFrameworkStep,
target: Target,

pub fn init(
    b: *std.Build,
    deps: *const SharedDeps,
    target: Target,
) !GhosttyXCFramework {
    // cmux fork: only construct the slices we'll actually wrap. The iOS
    // and universal-macOS lookups call LibCInstallation.findNative, which
    // probes Xcode's iOS SDK via xcrun. Hosts using Command Line Tools
    // SDK only (a workaround for Xcode 26's arm64e-only libSystem.tbd
    // breaking zig's host link) don't have iOS SDK installed. Skipping
    // unused lookups also speeds up native-only rebuilds.
    const macos_universal = if (target == .universal)
        try GhosttyLib.initMacOSUniversal(b, deps)
    else
        null;

    const macos_native = if (target == .native)
        try GhosttyLib.initStatic(b, &try deps.retarget(
            b,
            Config.genericMacOSTarget(b, null),
        ))
    else
        null;

    const ios = if (target == .universal)
        try GhosttyLib.initStatic(b, &try deps.retarget(
            b,
            b.resolveTargetQuery(.{
                .cpu_arch = .aarch64,
                .os_tag = .ios,
                .os_version_min = Config.osVersionMin(.ios),
                .abi = null,
            }),
        ))
    else
        null;

    const ios_sim = if (target == .universal)
        try GhosttyLib.initStatic(b, &try deps.retarget(
            b,
            b.resolveTargetQuery(.{
                .cpu_arch = .aarch64,
                .os_tag = .ios,
                .os_version_min = Config.osVersionMin(.ios),
                .abi = .simulator,

                // We force the Apple CPU model because the simulator
                // doesn't support the generic CPU model as of Zig 0.14 due
                // to missing "altnzcv" instructions, which is false. This
                // surely can't be right but we can fix this if/when we get
                // back to running simulator builds.
                .cpu_model = .{ .explicit = &std.Target.aarch64.cpu.apple_a17 },
            }),
        ))
    else
        null;

    // Generate a headers directory with only ghostty.h and the module
    // map. We can't use include/ directly because it also contains the
    // libghostty-vt headers under include/ghostty/, which would trigger
    // "umbrella header does not include header" warnings from Clang's
    // module system.
    const wf = b.addWriteFiles();
    _ = wf.addCopyFile(b.path("include/ghostty.h"), "ghostty.h");
    _ = wf.addCopyFile(b.path("include/module.modulemap"), "module.modulemap");
    const headers = wf.getDirectory();

    // The xcframework wraps our ghostty library so that we can link
    // it to the final app built with Swift.
    const xcframework = XCFrameworkStep.create(b, .{
        .name = "GhosttyKit",
        .out_path = "macos/GhosttyKit.xcframework",
        .libraries = switch (target) {
            .universal => &.{
                .{
                    .library = macos_universal.?.output,
                    .headers = headers,
                    .dsym = macos_universal.?.dsym,
                },
                .{
                    .library = ios.?.output,
                    .headers = headers,
                    .dsym = ios.?.dsym,
                },
                .{
                    .library = ios_sim.?.output,
                    .headers = headers,
                    .dsym = ios_sim.?.dsym,
                },
            },

            .native => &.{.{
                .library = macos_native.?.output,
                .headers = headers,
                .dsym = macos_native.?.dsym,
            }},
        },
    });

    return .{
        .xcframework = xcframework,
        .target = target,
    };
}

pub fn install(self: *const GhosttyXCFramework) void {
    const b = self.xcframework.step.owner;
    self.addStepDependencies(b.getInstallStep());
}

pub fn addStepDependencies(
    self: *const GhosttyXCFramework,
    other_step: *std.Build.Step,
) void {
    other_step.dependOn(self.xcframework.step);
}
